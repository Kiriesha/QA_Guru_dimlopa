plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.11.2"
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

def selenideVersion = "6.9.0",
    junitVersion = "5.9.1",
    junitParamsVersion = "5.8.1",
    slf4jVersion = "1.7.36",
    javaFakerVersion = "1.0.2",
    allureSelenideVersion = "2.20.1",
    allureRestAssuredVersion = "2.20.1",
    ownerVersion = "1.0.12",
    restAssuredVersion = "5.3.0",
    assertjVersion = "3.22.0",
    jsonPathVersion = "2.7.0"

allure {
    report {
        version.set("2.20.1")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.20.1")
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "com.github.javafaker:javafaker:$javaFakerVersion",
            "io.qameta.allure:allure-selenide:$allureSelenideVersion",
            "io.qameta.allure:allure-rest-assured:$allureRestAssuredVersion",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "com.jayway.jsonpath:json-path:$jsonPathVersion",
            "org.junit.jupiter:junit-jupiter-params:$junitParamsVersion"
    )
    testRuntimeOnly(
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion")
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}